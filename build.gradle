plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'nu.studer.jooq' version '9.0'
}

group = 'api-server'
version = '0.0.1-SNAPSHOT'
description = 'api-server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def xrayVersion = findProperty('xrayVersion') ?: '2.14.0'

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.2')
    testImplementation platform('org.springframework.boot:spring-boot-dependencies:3.5.2')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    /* Querydsl */
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6' // JSON serializer

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'


    /* Archunit Test */
    testImplementation "com.tngtech.archunit:archunit-junit5:1.3.0"

    /* jOOQ*/
    jooqGenerator 'org.jooq:jooq:3.19.10'
    jooqGenerator 'org.jooq:jooq-meta:3.19.10'
    jooqGenerator 'org.jooq:jooq-codegen:3.19.10'
    jooqGenerator 'org.jooq:jooq-meta-extensions:3.19.10'

    jooqGenerator 'org.postgresql:postgresql:42.6.0'

    /* AWS X-Ray */
    implementation "com.amazonaws:aws-xray-recorder-sdk-core"

    implementation "com.amazonaws:aws-xray-recorder-sdk-spring"
    implementation "org.springframework.boot:spring-boot-starter-aop"

    implementation "com.amazonaws:aws-xray-recorder-sdk-sql-postgres"
    runtimeOnly "org.postgresql:postgresql"

    implementation platform("com.amazonaws:aws-xray-recorder-sdk-bom:${xrayVersion}")
    implementation "com.amazonaws:aws-xray-recorder-sdk-core"
    implementation "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2"
    implementation "com.amazonaws:aws-xray-recorder-sdk-spring"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "com.amazonaws:aws-xray-recorder-sdk-sql-postgres"
    implementation "com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2"
    implementation "com.amazonaws:aws-xray-recorder-sdk-aws-sdk"

    /* Spring Cloud */
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:3.1.3'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws-secrets-manager-config:2.2.6.RELEASE'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.named("jacocoTestReport")
}

// CI 환경(GitHub Actions)에서는 generateJooq 태스크 실행 안 함
// afterEvaluate로 감싸서 모든 태스크가 생성된 후 접근하도록 변경
afterEvaluate {
    if (tasks.findByName('generateJooq')) {
        tasks.named('generateJooq') {
            onlyIf {
                System.getenv('CI') == null
            }
        }
    }
}

/* Jacoco Test Setting */
jacoco {
    toolVersion = "0.8.14"
}

def jacocoExcludes = [
        '**/*Application*',
        '**/*Dto*',
        '**/*VO*',
        '**/*Vo*',
        '**/*Request*',
        '**/*Response*',
        '**/*Content*',
        '**/*Result*',
        '**/*Meta*',
        '**/*Exception*',
        '**/*Error*',
        '**/*Config*',
        '**/*Configuration*',
        '**/*Properties*',
        '**/*Interceptor*',
        '**/*Filter*',
        '**/*Advice*',
        '**/*Handler*',
        '**/*Mapper*',
        '**/*Assembler*',
        '**/*Converter*',
        '**/*Constant*',
        '**/*Constants*',
        '**/*Enum*',
        // jOOQ 생성 파일들 제외 (패키지 경로 기반)
        '**/site/holliverse/*/query/jooq/**',
        '**/site/holliverse/*/query/jooq/tables/**',
        '**/site/holliverse/*/query/jooq/tables/records/**',
        '**/site/holliverse/*/query/jooq/enums/**',

        // jOOQ 내부 관리용 파일들
        '**/*DefaultCatalog*',
        '**/*DefaultSchema*',
        '**/*Keys*',
        '**/*Indexes*',
        '**/*Tables*',
        '**/generated/**',
        '**/dto/**',
        '**/*Dao*',
        '**/web/dto/**',
        '**/shared/domain/model/**',
        '**/auth/jwt/**',
        '**/shared/security/**',
        '**/Q*.class',
        '**/Q*.java',
        '**/*Querydsl*',
        '**/*ArchitectureRules*',
]

tasks.named("jacocoTestReport", JacocoReport) {
    dependsOn tasks.named("test")

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExcludes)
            })
    )
}
tasks.named("jacocoTestCoverageVerification", JacocoCoverageVerification) {
    enabled = false

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExcludes)
            })
    )
    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}

/* jOOQ Code Generation Setting */
jooq {
    version = '3.19.10'

    configurations {
        main {  // 'main' 소스셋에 적용
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    //
                    driver = 'org.postgresql.Driver'
                    url = System.getenv('DB_URL') ?: 'jdbc:postgresql://localhost:5432/holliverse'
                    user = System.getenv('DB_USER_ADMIN') ?: 'postgres'
                    password = System.getenv('DB_PASS_ADMIN') ?: 'postgres'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'

                        // 제외할 테이블
                        excludes = 'flyway_schema_history'
                    }
                    target {
                        packageName = 'site.holliverse.admin.query.jooq'
                        directory = 'src/main/generated'
                    }
                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}
